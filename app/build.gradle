/*
 * Copyright (c) 2013-2016 Shaleen Jain <shaleen.jain95@gmail.com>
 *
 * This file is part of UPES Academics.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.bugsnag.android.gradle'
apply plugin: 'com.google.gms.oss.licenses.plugin'

// query git for the SHA, Tag and commit count. Use these to automate versioning.
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = 2007050 +
        Integer.parseInt('git rev-list --count HEAD --no-merges'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId 'com.shalzz.attendance'
        minSdkVersion 14
        targetSdkVersion 28
	    versionCode gitCommitCount
	    versionName gitTag

        resConfigs "en"
    }

    compileOptions {
        incremental true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    play {
        jsonFile = file('../play-service-account-key.json')
        uploadImages = true
        track = 'production'
    }

    signingConfigs {
        // You must set up an environment var before release signing
        // Run: export APP_KEY={password}
        release {
            storeFile file("keystore/release.keystore")
            keyAlias "attendance keystore"
            storePassword "$System.env.APP_KEY"
            keyPassword "$System.env.APP_KEY"
        }

        debug {
            storeFile file('keystore/debug.keystore')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            ext.enableBugsnag = false
            resValue "string", "app_name", "College Academics (debug)"
            resValue "string", "contentAuthority", defaultConfig.applicationId + '.debug.provider'
            resValue "string", "account_type", "com.shalzz.debug"
            buildConfigField "String", "ACCOUNT_TYPE", '"com.shalzz.debug"'
        }
        release {
            minifyEnabled true
            useProguard true
            resValue "string", "app_name", "College Academics"
            resValue "string", "contentAuthority", defaultConfig.applicationId + '.provider'
            resValue "string", "account_type", "com.shalzz"
            buildConfigField "String", "ACCOUNT_TYPE", '"com.shalzz"'
//            FileCollection proGuardFileCollection = files { file('./proguard').listFiles() }
            proguardFiles(file('./proguard').listFiles())
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig signingConfigs.release
        }
    }

    sourceSets {

        // Main source set is Kotlin!
        main.java.srcDirs += 'src/main/kotlin'

        // Test helps
        def commonTestDir = 'src/commonTest/java'
        test.java.srcDir commonTestDir
        androidTest.java.srcDir commonTestDir
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        disable 'InvalidPackage'
    }

    //Needed because of this https://github.com/ReactiveX/RxJava/issues/4445
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    // Needed because of this https://github.com/robolectric/robolectric/issues/{1399, 3826}
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    dependencies {
        final SUPPORT_LIBRARY_VERSION = '27.1.1'
        final DAGGER_VERSION = '2.17'
        final ESPRESSO_VERSION = '3.1.0-alpha1'
        final RUNNER_VERSION = '0.4'
        final RETROFIT_VERSION = '2.4.0'
        final BUTTERKNIFE_VERSION = '9.0.0-SNAPSHOT'
        final ROOM_VERSION = "2.0.0-rc01"

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        implementation 'com.google.firebase:firebase-core:16.0.3'
        implementation 'com.google.firebase:firebase-analytics:16.0.3'
        implementation 'com.google.firebase:firebase-messaging:17.3.0'

        implementation 'com.google.android.gms:play-services-oss-licenses:15.0.1'
        implementation "com.google.android.material:material:1.0.0"

        implementation "androidx.core:core:1.0.0"
        implementation "androidx.appcompat:appcompat:1.0.0"
        implementation "androidx.fragment:fragment:1.0.0"
        implementation "androidx.recyclerview:recyclerview:1.0.0"
        implementation "androidx.preference:preference:1.0.0"
        implementation "androidx.annotation:annotation:1.0.0"
        implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
        implementation 'androidx.constraintlayout:constraintlayout-solver:1.1.3'

        implementation 'com.android.billingclient:billing:1.0'

        def daggerCompiler = "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
        implementation  "com.google.dagger:dagger:$DAGGER_VERSION"
        compileOnly 'javax.annotation:jsr250-api:1.0'
        kapt daggerCompiler
        testAnnotationProcessor daggerCompiler
        androidTestAnnotationProcessor daggerCompiler

        implementation 'com.jakewharton.timber:timber:4.7.1'
        implementation "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
        kapt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

        implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
        implementation 'io.reactivex.rxjava2:rxjava:2.1.3'

        implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
        implementation "com.squareup.retrofit2:converter-moshi:$RETROFIT_VERSION"
        implementation 'com.squareup.moshi:moshi:1.7.0'
        kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.7.0'
        implementation 'com.squareup.moshi:moshi-adapters:1.7.0'
        implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

        implementation 'com.malinskiy:materialicons:1.0.2'
        implementation 'com.github.amlcurran.showcaseview:library:5.4.0'
        implementation 'com.github.afollestad.material-dialogs:core:0.8.5.5'

	    //noinspection GradleDynamicVersion
        implementation 'com.bugsnag:bugsnag-android:4.+'

        // Room Persistence
        implementation "androidx.room:room-runtime:$ROOM_VERSION"
        kapt "androidx.room:room-compiler:$ROOM_VERSION"
        implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
        testImplementation "androidx.room:room-testing:$ROOM_VERSION"

        def jUnit = "junit:junit:4.12"
        def mockito = "org.mockito:mockito-core:2.6.2"

        // Instrumentation test dependencies
        androidTestImplementation jUnit
        androidTestImplementation mockito
        androidTestImplementation "androidx.annotation:annotation:1.0.0-rc01"
        androidTestImplementation "androidx.test.espresso:espresso-contrib:$ESPRESSO_VERSION"
        androidTestImplementation "androidx.test.espresso:espresso-core:$ESPRESSO_VERSION"
        androidTestImplementation "androidx.test:runner:1.1.0-alpha1"
        androidTestImplementation "androidx.test:rules:1.1.0-alpha1"
        androidTestImplementation "com.google.code.findbugs:jsr305:3.0.0"

        // Unit tests dependencies
        testImplementation jUnit
        testImplementation mockito
        testImplementation "org.robolectric:robolectric:3.8"
        testImplementation 'commons-io:commons-io:1.3.2'
        testImplementation "com.google.truth:truth:0.27"
    }
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'
